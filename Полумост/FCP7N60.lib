*$
**************** Power Discrete MOSFET Electrical Circuit Model *****************
** Product Name: FCP7N60
** 600V N-Channel MOSFET and TO-220
** Model Type: BSIM3V3
**-------------------------------------------------------------------------------
.SUBCKT FCP7N60 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5    DbodyMOD
Dbreak 5 11  DbreakMOD
Ebreak 11 7 17 7 660
Lgate 1 9    1.13e-9
Ldrain 2 5   1.44e-9
Lsource 3 7  1.65e-9
RLgate 1 9   11.3
RLdrain 2 5  14.4
RLsource 3 7 16.5
Rgate 9 6    0.5
It 7 17      1
Rbreak 17 7  RbreakMOD 1
.MODEL RbreakMOD RES (TC1=1.02e-3 TC2=-1.35e-6)
.MODEL DbodyMOD D (IS=5.05e-13  N=1      RS=2.38e-2   TRS1=1.0e-3 TRS2=3.15e-6
+ CJO=2.32e-9      M=0.60       VJ=0.84  TT=2.10e-7   XTI=3       EG=1.18)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1.0e-3 TRS2=1e-6)
Rdrain 5 16 RdrainMOD 0.53
.MODEL RdrainMOD RES (TC1=8.1e-3 TC2=1.62e-5)
M_BSIM3 16 6 7 7 Bsim3 W=1.08 L=2.0e-6 NRS=1
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 PARAMCHK=1 NQSMOD=0
+ TOX=980e-10     XJ=1.4e-6      NCH=1.14e17
+ U0=700          VSAT=5.0e5     DROUT=1.0 
+ DELTA=0.1       PSCBE2=0       RSH=5.89e-3
+ VTH0=4.45       VOFF=-0.1      NFACTOR=1.1
+ LINT=1.12e-7    DLC=1.12e-7    FC=0.5
+ CGSO=1.05e-15   CGSL=0         CGDO=1.05e-13
+ CGDL=9.15e-10   CJ=0           CF=0
+ CKAPPA=0.2      KT1=-1.44      KT2=0
+ UA1=-2.05e-9    NJ=10)
.ENDS

******************** Power Discrete MOSFET Thermal Model ************************
** 600V N-Channel MOSFET and TO-220
**-------------------------------------------------------------------------------
.SUBCKT FCP7N60_Thermal TH TL
CTHERM1 TH 6 1.04e-5
CTHERM2 6 5  1.58e-4
CTHERM3 5 4  8.64e-4
CTHERM4 4 3  4.82e-3
CTHERM5 3 2  2.82e-2
CTHERM6 2 TL 2.62e-1
RTHERM1 TH 6 1.10e-3
RTHERM2 6 5  5.78e-3
RTHERM3 5 4  1.83e-2
RTHERM4 4 3  9.20e-2
RTHERM5 3 2  5.51e-1
RTHERM6 2 TL 8.32e-1
.ENDS FCP7N60_Thermal 
**-------------------------------------------------------------------------------
** Creation: Jun.-13-2016   Rev.: 0.0
** Fairchild Semiconductor
